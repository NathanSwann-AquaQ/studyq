# 976. Largest Perimeter Triangle

https://leetcode.com/problems/largest-perimeter-triangle/

> Given an integer array `nums`, return the largest perimeter of a triangle with a non-zero area, formed from three of these lengths. If it is impossible to form any triangle of a non-zero area, return 0.


## Solutions

The length of the perimeter of a triangle is the sum of the lengths of its sides.

The longest perimeter will be the sum of the three largest items – 
provided that they can form a triangle. 

They cannot form a triangle if the largest length exceeds the sum of the smaller two.
If it does, it cannot form a triangle with any of the remaining lengths either, because they are shorter still.
It can therefore be discarded.

We have the start of a plan.
Sort the lengths descending. Try the first three. 


### Recursion

```q
q)ex1:2 1 2           / triangle
q)ex2:1 2 1           / not
q)show ex3:10?10      / ???
8 1 9 5 4 6 6 1 8 5
q)desc ex3
9 8 8 6 6 5 5 4 1 1
q){x[0]<sum x 1 2}desc ex3
1b
q){$[x[0]<sum x 1 2;3#x;.z.s 1_ x]}desc ex3
9 8 8
q)  
0N 0N 0N
```

If no triangle is found, the result is 3 nulls. 
No problem; they sum just fine.

```q
q)s0:sum{$[x[0]<sum x 1 2;3#x;.z.s 1_ x]}desc@
q)s0 each(ex1;ex2;ex3)
5 0 25
```

### While

Or we could just walk along the sorted list – until we find a triangle or fall off the end.

```q
s1:{i:til 3; 
  while[(not all null x i)and(x i 0)>=sum x i 1 2; i+:1]; 
  sum x i } desc@
```

The same algorithm can be expressed with the While iterator. 
Start with the sorted list and three indices.
Use [Amend At](https://code.kx.com/q/ref/amend) to increment the indices.

```q
q)(desc ex3;til 3)
9 8 8 6 6 5 5 4 1 1
0 1 2

q)@[;1;1+](desc ex3;til 3)
9 8 8 6 6 5 5 4 1 1
1 2 3
```

Now apply the [While form of Over](https://code.kx.com/q/ref/accumulators/#while) with a test that returns zero 
if the three lengths make a triangle.

```q
q){{x[0]>=sum x 1 2}x y}. @[;1;1+](desc ex3;til 3)
0b
q)sum .[@]({{x[0]>=sum x 1 2}x y}.) @[;1;1+]/(desc ex3;til 3)
25
q)s2:sum .[@] {({{x[0]>=sum x 1 2}x y}.) @[;1;1+]/x} (;til 3) desc@
q)s2 each(ex1;ex2;ex3)
5 0 25
```

The `(;til 3)desc@` yields the sorted list and the first three indexes; the While expression increments the indexes; the `.[@]` selects the indexed list items and `sum`, well, sums them.


## Alternatives

```q
/ Cillian Reilly
c0:{sum w first where{x<y+z}./:w:distinct 2_{1_x,y}\[3#0;]desc{raze value[x]#'key x}3&count each group x}
c1:{(r;0)0<type r:(2<count::){(1_x;sum s){x<y+z}. s:3#x}/desc x}
```

Note in `c1` the use of `{x<y+z}.` to test the triple for triangularity; then the use of the result to select the result from either `1_x` or `sum s`. 

Where `s1` and `s2` increment the index triple, `c1` and `s0` repeatedly drop the first item. 

```q
/ George Berkeley
g2:{[nums]  
  perim:{[i;ns](0;sum s){x<y+z}. s:3#i _ns}.;
  s0:(0;desc nums;neg[2]+count nums;0);
  last {[s](s[0]<s[2]) and s[3]=0}{[s;p](s[0]+1;s[1];s[2];p 2#s)}[;perim]/s0}

/ Stephen Taylor
s0:sum{$[x[0]<sum x 1 2;3#x;.z.s 1_ x]}desc@                        / recursion

s1:{i:til 3; 
  while[(not all null x i)and(x i 0)>=sum x i 1 2; i+:1]; 
  sum x i } desc@                                                   / while 1

s2:sum .[@] {({{x[0]>=sum x 1 2}x y}.) @[;1;1+]/x} (;til 3) desc@   / while 2
```

## Performance

Validate:

```q
q)ex4:10000?1000000  / per LeetCode constraints

q)(c0;c1;g2;s0;s1;s2)@/:\:(ex1;ex2;ex3;ex4)
5 0 25 2999307
5 0 25 2999307
5 0 25 2999307
5 0 25 2999307
5 0 25 2999307
5 0 25 2999307
```

```q
q){x,system"ts:100 ",string[x]," ex4"}each `c0`c1`g2`s0`s1`s2
`c0 693 1557568
`c1 17  655616
`g2 18  655728
`s0 16  655584
`s1 16  655584
`s2 16  655584
```

---

Contributors

* Cillian Reilly
* George Berkeley
* Stephen Taylor