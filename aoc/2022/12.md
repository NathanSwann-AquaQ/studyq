# Day 12: Hill Climbing Algorithm 

> You try contacting the Elves using your handheld device, but the river you're following must be too low to get a decent signal.
> 
> You ask the device for a heightmap of the surrounding area (your puzzle input). The heightmap shows the local area from above broken into a grid; the elevation of each square of the grid is given by a single lowercase letter, where a is the lowest elevation, b is the next-lowest, and so on up to the highest elevation, z.

Advent of Code 2022 [Day 12](https://adventofcode.com/2022/day/12)


## Ingestion

```q
q)show inp: read0 `:test/12.txt
"Sabqponm"
"abcryxxl"
"accszExk"
"acctuvwj"
"abdefghi"
```

## Part 1

We can use the `vs` keyword to convert indices of a razed matrix into co-ordinate pairs. 

```q
Shp: count each 1 first\inp                                         /map shape
`Start`End set'Shp vs/:raze[inp]?"SE";                              /start & end coords
```

We shall need to consult a height map.

```q
Map: ./[;(Start;End);:;0 25] -97+6h$inp                             /height map
```

Our strategy is to start from the end point and with each step identify the unvisited accessible neighbours until they include the start point.
The number of steps we have taken will be the shortest route.

We can represent the ongoing search as a pair of lists. 
The first is the points visited; the second is a subset of it: the points last visited.

```q
Is: 2 1 2#End                                                       /initial state
```

Each step extends the list of visited points.
We keep taking steps until the start point shows up.

```q
adj: (0 1;1 0;0 -1;-1 0)+\:                                         /adjacent points
filter: {y where not[null h] and (-2+Map . x)< h: Map ./:y}
step: {(x,;::)@\:except[;x]distinct raze{filter[x] adj x}each y}.  
```

The `adj` projection returns adjacent points, but some can be inaccessible: either off the map, or failing the height test. 
The `filter` function removes any inaccessible points. 
When `filter` has pruned the neighbours of each point last visited, the pruned lists are razed, any duplicates removed, and also any already visited.
The result is the extended list of visited points, and the points just now visited. 

The [While iterator](https://code.kx.com/q/ref/accumulators/#while) 
keeps taking steps until the starting point has been visited. 
It remains only to count the steps.


```q
-1+count(not Start in last@) step\ Is                               /part 1
```


## Part 2

For Part 2 we need only replace the start point with a list of the points where the map shows an `"a"`.

```q
a: Shp vs/:where raze inp="a"                                       /"a" coords
-1+count(not any a in last@) step\ Is                               /part 2
```


## Contributors

* Cillian Reilly
* Stephen Taylor

