# Day 10: Cathode-Ray Tube 

> You avoid the ropes, plunge into the river, and swim to shore.
>
> The Elves yell something about meeting back up with them upriver, but the river is too loud to tell exactly what they're saying. They finish crossing the bridge and disappear from view.
>
> Situations like this must be why the Elves prioritized getting the communication system on your handheld device working. You pull it out of your pack, but the amount of water slowly draining from a big crack in its screen tells you it probably won't be of much immediate use.

Advent of Code 2022 [Day 10](https://adventofcode.com/2022/day/10)


## Ingestion

```q
q)show inp: read0 `:test/10.txt  
q)inp
"addx 15"
"addx -11"
"addx 6"
"addx -3"
"addx 5"
"addx -1"
"addx -8"
"addx 13"
"addx 4"
"noop"
"addx -1"
..
```

## Part 1

The `noop` operation completes in a single cycle; the `addx` in two, with the X register incrementing on the second. 

If we replace both commands with zeros we have the series of changes to the X register; cumulative sums give the series of X-register values.

```q
q)show i:1+\get ssr/[;("addx";"noop");"0"]" "sv inp
1 16 16 5 5 11 11 8 8 13 13 12 12 4 4 17 17 21 21 21 20 20 25 25 24 24 29 29 28 28 33 33 32 32 37 37 36 36 1 1 2 ..
```

Or we can use the q interpreter itself.

```q
q)noop:0;addx:0,
q)show i:1+\get","sv inp
1 16 16 5 5 11 11 8 8 13 13 12 12 4 4 17 17 21 21 21 20 20 25 25 24 24 29 29 28 28 33 33 32 32 37 37 36 36 1 1 2 ..
```

It remains only to index into the specified cycles.

```q
q)show c:20+40*til 6        /cycles
20 60 100 140 180 220
q)i c-2                     /values at start of cycles
21 19 18 21 16 18
q)sum i[c-2]*c:20+40*til 6  /part 1
13140
```

## Part 2

The horizontal pixel co-ords of a 6×40 screen:
```
q)240#til 40
0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 0 1..
```

The pixels of `i` that light up:

```q
q)(240#til 40)within'-1 1+/:0^prev i
11001100110011001100110011001100110011001110001110001110001110001110001110001110111100001111000011110000111100001..
```

Or:

```q
q)2>abs(-1_1,i)-240#til 40
11001100110011001100110011001100110011001110001110001110001110001110001110001110111100001111000011110000111100001..

q)show".#"40 cut 2>abs(-1_1,i)-240#til 40
"##..##..##..##..##..##..##..##..##..##.."
"###...###...###...###...###...###...###."
"####....####....####....####....####...."
"#####.....#####.....#####.....#####....."
"######......######......######......####"
"#######.......#######.......#######....."
```

The puzzle input will produce recognizable characters on the CRT. 

```q
.d10.ocr:()!();
.d10.ocr[529680320]: "A"
.d10.ocr[1067881856]: "B"
.d10.ocr[512103552]: "C"
..

.d10.ocr 2 sv/:raze each 5 cut flip 40 cut 2>abs(-1_1,i)-240#til 40
```

## Solutions

```q
i:1+\get ssr/[;("addx";"noop");"0"]" "sv inp   /X-register values
sum i[c-2]*c:20+40*til 6                       /part 1
show".#"40 cut 2>abs(-1_1,i)-240#til 40        /part 2
```

## Contributors

* András Dőtsch
* Péter Györök
* Cillian Reilly
* Stephen Taylor
* Zsolt Venczel
