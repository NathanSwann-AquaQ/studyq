# Day 7: No Space Left On Device 

> You can hear birds chirping and raindrops hitting leaves as the expedition proceeds. Occasionally, you can even hear much louder sounds in the distance; how big do the animals get out here, anyway?

Advent of Code 2022 [Day 7](https://adventofcode.com/2022/day/7)

This problem requires us to 

1.  infer the filesystem hierarchy from the log of shell commands and responses
1.  analyse the hierarchy

We’ll start with a verbose solution as a way to gain insight into the problem, then move on to study a terser solution.

## Ingestion

```q
q)inp: read0 `:input/07.txt  
q)4#cr: where[inp[;0]="$"]_ inp  /command:response list
,"$ cd /"
("$ ls";"dir a";"14848514 b.txt";"8504156 c.dat";"dir d")
,"$ cd a"
("$ ls";"dir e";"29116 f";"2557 g";"62596 h.lst")
```

Cut groups the lines: each item is a command optionally followed by a list of resposnes.

## Solution 1

We now write a ‘shell explorer’ `se`. 
It reads the grouped log and records what it learns in a table describing the filesystem. 

The shell explorer `se` starts with an initial state: a template filesystem table, and a working directory. 

```q
q)show fst:1!flip `pth`typ`siz!enlist each(1#"/";`d;0N) /file system template
pth | typ siz
----| -------
,"/"| d
```

Our initial state is `(fst;"")`. 
It’s the first argument of `se`, the second is a command-response list.
So we can construct the filesystem table:

```q
FS:first (fst;"") se/cr  /discover file system
```

The shell explorer interprets the `cd` and `ls` commands to update either the working directory or the filesystem table.

```q
/shell explorer
se: {[state;cr]  /state; commend/response
  fs: state 0; path: state 1; cmd: cr[0;2 3];
  if[cmd~"cd"; path: cd[path] 5_first cr];
  if[cmd~"ls"; fs,: ls[path] 1_cr];
  (fs;path) }  

/shell commands
fp: {[sep;p]p,(sep<>last p)#sep}["/"]  /full path
cd: {[p;s]$["/"=s 0;s;".."~2#s;"/"sv -1_"/"vs p;fp[p],s]}  /new path
ls: {[path;strings]
  m: " "vs'strings;
  typ: `f`d null siz: "J"$m[;0];
  pth: fp[path],/:m[;1];
  ([pth]typ;siz) }
```

Keying the filesystem table on `pth` means that it can be updated in `se` with a simple Join: `fs,: ls[path] 1_cr`.

```q
q)show FS:first (fst;"") se/cr
pth       | typ siz
----------| ------------
,"/"      | d
"/a"      | d
"/b.txt"  | f   14848514
"/c.dat"  | f   8504156
"/d"      | d
"/a/e"    | d
"/a/f"    | f   29116
"/a/g"    | f   2557
"/a/h.lst"| f   62596
"/a/e/i"  | f   584
"/d/j"    | f   4060174
"/d/d.log"| f   8033020
"/d/d.ext"| f   5626152
"/d/k"    | f   7214296
```

Take the directory paths, longest first, and sum the sizes of their file children.
Write the directory sizes back into `FS`.

```q
q)/directory sizes
q)dp: {x idesc count each x}exec pth from FS where typ=`d  /dir paths
q)ds: (0!FS){sum x[`siz]where (`f=x`typ)&x[`pth]like fp[y],"*"}/:dp
q)show FS: FS upsert([pth:dp]siz:ds)
pth       | typ siz
----------| ------------
,"/"      | d   48381165
"/a"      | d   94853
"/b.txt"  | f   14848514
"/c.dat"  | f   8504156
"/d"      | d   24933642
"/a/e"    | d   584
"/a/f"    | f   29116
"/a/g"    | f   2557
"/a/h.lst"| f   62596
"/a/e/i"  | f   584
"/d/j"    | f   4060174
"/d/d.log"| f   8033020
"/d/d.ext"| f   5626152
"/d/k"    | f   7214296
```

```q
/part 1
exec sum siz from FS where siz<=100000,typ=`d
/part 2
reqd: 0|30000000-70000000-FS[enlist"/";`siz]  /required space
first exec siz from `siz xasc select from FS where typ=`d,siz>reqd
```

How can we improve the above?
We could usefully omit the directory entries from `FS`: the directory paths and sizes can be derived from the filepaths. 
We could represent everything the shell explorer learns with a dictionary: filepaths => sizes.

Rather than rework Solution 1, move on to Solution 2, from András Dőtsch and Péter Györök – which follows the same thought.


## Solution 2

```q
p:(1#`:) {$[y~"$ cd /";1#x;y~"$ cd ..";-1_x;y like"$ cd *";x,.Q.dd[last x]`$last" "vs y;x]} \ inp
s:"J"$first@'(" "vs/:inp)
d:exec sum s by p from ungroup ([]p;s)
sum d where d<=100000
min d where d>=d[`:]-40000000
```

The definition of `p` is a ruthlessly stripped version of the shell explorer.
It ignores file sizes and traverses the log, updating only the current working directory.

For this it uses the Scan iterator in the form `x f\y` where `x` is the initial state and `y` is a list of right arguments from which `f` derives new states.

```q
q)p
,`:
,`:
,`:
,`:
,`:
,`:
`:`:/a
`:`:/a
`:`:/a
`:`:/a
`:`:/a
`:`:/a
`:`:/a`:/a/e
`:`:/a`:/a/e
`:`:/a`:/a/e
`:`:/a
,`:
`:`:/d
`:`:/d
`:`:/d
`:`:/d
`:`:/d
`:`:/d
```

Notice that the directory path is returned as a symbol vector of the paths of the directory and all its ancestors.

The definition of `s` rips through the log, ignoring everything *except* file sizes.

**Ingenuity alert** `([]p;s)` matches each file size to its directory chain; `ungroup` converts that to a 1:1 mapping of directory path to file size.
It remains only to `exec sum by p from` the result to get `d`.

```q
q)d
:    | 48381165
:/a  | 94853
:/a/e| 584
:/d  | 24933642
```

Where Solution 1 starts by tabulating everything that could be learned from the log, Solution 2 rips out only what it needs to answer the question.

This is predator thinking. 
A superb example of the difference between competence and mastery.

## Contributors

* András Dőtsch
* Péter Györök
* Stephen Taylor