# Day 9: Rope Bridge 

> This rope bridge creaks as you walk along it. You aren't sure how old it is, or whether it can even support your weight. It seems to support the Elves just fine, though. The bridge spans a gorge which was carved out by the massive river far below you.
>
> You step carefully; as you do, the ropes stretch and twist. You decide to distract yourself by modeling rope physics; maybe you can even figure out where not to step.

Advent of Code 2022 [Day 9](https://adventofcode.com/2022/day/9)


## Ingestion

```q
inp: read0 `:test/09.txt  
```

## Head positions

Any move of the rope’s head by one space might require a move of the tail. 
So, while it would be easy to move the head several spaces each time, we shall interpret e.g. `R 4` as four moves to the right, and take cumulative sums to get the head positions. 

```q
q)show s: sums("RLDU"!(1 0;-1 0;0 -1;0 1))where .[!] ("CJ";" ")0:inp
1 0
2 0
3 0
4 0
4 1
4 2
..
```

Notice above how Apply Dict makes a dictionary of the two vectors returned by [Load CSV](https://code.kx.com/q/ref/file-text/#load-csv), which is then passed to `where`.
You will be familiar with `where` on a boolean vector.

```q
q)where 101b
0 2
```

You may also have seen that the rule for `where` extends to integers: instead of returning either one or zero copies of each index, it returns the specified number.

```q
q)where 3 0 4
0 0 0 2 2 2 2
```

The same rule works also for dictionaries.

```q
q)where `cow`sheep`cat!2 0 3
`cow`cow`cat`cat`cat
```

In each case, `where` returns copies of its argument’s indices: the indices of a vector `v` are `til v`; of a dictionary `d`, they are `key d`.


## Tail positions

The tail starts from the same position as the head: `0 0`.
At each new head position we decide whether to adjust the position of the tail.

```q
move: {$[1<max abs d:y-x;x+signum d;x]}\[0 0;]
```

In the lambda above, `x` is the tail position, `y` the head position, and `d` their separation. A new tail position is returned. 
This is done *successively*, to produce the tail positions for each head position. 

```q
q)0 0 {$[1<max abs d:y-x;x+signum d;x]}\s
0 0
1 0
2 0
3 0
3 0
4 1
4 2
..
```

It remains only to count the distinct positions.

```q
q)count distinct move s             /part 1  
13
```

## Part 2

In Part 2 the head movements recapitulate the previous tail movements. 
That is, the result of `move` becomes its next argument – eight times.

```q
q)9 move\s
1 0 2 0 3 0 4 0 4 1 4 2 4 3 4 4 3 4 2 4 1 4 1 3 2 3 3 3 4 3 5 3 5 2 4 2 3 2 2..
0 0 1 0 2 0 3 0 3 0 4 1 4 2 4 3 4 3 3 4 2 4 2 4 2 4 2 4 3 3 4 3 4 3 4 3 4 3 3..
0 0 0 0 1 0 2 0 2 0 3 1 3 1 4 2 4 2 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3..
0 0 0 0 0 0 1 0 1 0 2 1 2 1 3 2 3 2 3 2 3 2 3 2 3 2 3 2 3 2 3 2 3 2 3 2 3 2 3..
0 0 0 0 0 0 0 0 0 0 1 1 1 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
```

We can see the rope settling down.

```q
q)count distinct 9 move/s             /part 2
1

q)(count distinct::)each(9 move\s)1 9  /both parts
13 1
```


## Contributors

* András Dőtsch
* Nick Psaris
* Cillian Reilly

