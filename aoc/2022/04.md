# Day 4: Camp Cleanup 

> Space needs to be cleared before the last supplies can be unloaded from the ships, and so several Elves have been assigned the job of cleaning up sections of the camp. Every section has a unique ID number, and each Elf is assigned a range of section IDs.

Advent of Code 2022 [Day 4](https://adventofcode.com/2022/day/4)

## Ingestion

```q
q)show inp: read0`:test/04.txt  /assignments
"2-4,6-8"
"2-3,4-5"
"5-7,7-9"
"2-8,3-7"
"6-6,4-6"
"2-6,4-8"
```


## Part 1

Parse the assignments into a list of 2×2 matrices: each one a pair of ranges.

```q
q)show ass: "J"$''"-"vs''","vs' inp
2 4 6 8
2 3 4 5
5 7 7 9
2 8 3 7
6 6 4 6
2 6 4 8
q)ass 3
2 8
3 7
```

Keyword `within` tests the first range against the second.

```q
q).[within] ass 3
00b
```

Both have to be true to qualify.

```q
q)all .[within] ass 3
0b
```

But we also need to test the second range against the first. 
We can use `reverse\` to get both the pairs and their reverse.

```q
q)(reverse\) ass 3
2 8 3 7
3 7 2 8
```

So we can `reverse\` each assignment and test the assignments and their reverses.

```q
q)((all .[within]::)'') (reverse\') ass
00b
00b
00b
01b
10b
00b

q)sum any each((all .[within]::)'') (reverse\') ass
2i
```

## Part 2

The test for Part 2 is slightly different. 
Where we used `all` to test whether one range was wholly contained, we can use `any` to find if they overlap.

```q
q)sum any each ((any .[within]::)'') (reverse\') ass
4i
```

## Refactor

The similar solutions suggest a refactoring to defer application of `all` and `any`.

Use Each Each to apply `within .` to every assignmant and its reverse.

```q
q)(.[within]'') (reverse\') ass
00b 00b
00b 00b
01b 10b
00b 11b
11b 01b
01b 10b

q)(sum') (any'') (all'';any'')@\: (.[within]'') (reverse\') ass
2 4i
```

## Alternatives

There are other ways than `within` to test containment and overlap. 

Parse the assignments into a 2×2×N array.

```q
q)i: ("jj";"-")0:/:("**";",")0:`:test/04.txt
q)i 0
2 2 5 2 6 2
4 3 7 8 6 6
q)i 1
6 4 7 3 4 4
8 5 9 7 6 8
q)sum {0>=.[*]x-y} . i
2i
q)sum {0>=.[*]x-reverse y} . i
4i
```

Or, sorting each matrix simplifies the range tests:

```q
d4:{asc each "J"$"-"vs/:/:","vs/:x} 
{a:d4 x;sum(a[;1;1]<=a[;0;1]) or a[;0;0]=a[;1;0]} inp
{a:d4 x;sum a[;1;0]<=a[;0;1]} inp
```

## Contributors

* András Dőtsch
* Péter Györök
* Sujoy Rakshit
* Cillian Reilly
* Stephen Taylor

