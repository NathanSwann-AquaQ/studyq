# 3. Largest prime factor

## Problem

https://projecteuler.net/problem=3

The prime factors of 13195 are 5, 7, 13 and 29.
What is the largest prime factor of the number 600851475143?

## Solution

Candidate prime factors are the primes to the square root of 600851475143.

We apply Erastosthenes’ Sieve. 
(See [Finding primes](../essays/findingprimes.md).)

```q
N:600851475143
/candidates: primes to √N
kc:{(1#2;0b,1_x#10b)}                                                       / known primes; flag candidates
snp:{(x,n;y&count[y]#i<>til n:1+i:y?1b)}.                                   / sieve next prime
es:raze@[;1;1+where@] {{x>last first y}[floor sqrt x] snp/kc x}@            / Eratosthenes' sieve
c:es floor sqrt N                                                           / primes to √N

last c where not N mod c                                      				/ overcompute: try all
```

It remains to find the largest that is a factor of `N`.

```q
last c where not N mod c
```

## Loop?

The solution above is an example of ‘overcomputing’ typical of vector solutions.
It computes `N mod` for every candidate. 
Instead we could stop after the first zero result. 

```q
.[@] ({x mod y z}[N].) @[;1;1+]/(c;0)
```

In pseudocode:

```txt
i = 0
while mod[N;c[i]]
    i++
endwhile
return c[i]
```

The initial state is `(c;0)` – the candidates and the first index to try. 
The function `@[;1;1+]` is a unary projection of [Index At](https://code.kx.com/q/ref/apply#index-at).
It simply increments the index.
It is applied in the While form of the Over iterator; the truth function computes the modulo, and iteration stops when it returns zero. 

The final `.[@]` returns the indexed item of `c`.

But if we’re going to loop, we might as well lean into it and use the `while` keyword.

```q
i:count c; while[N mod c i-:1;]; c i
```

Did that help? Is the explicit While loop ‘paid for’ by the reduction in modulo computations?

Not in this case, because the largest prime factor comes low in the list of candidates.
The efficiency of implicit iteration justifies the overcompute.

```q
q)\t:10 last c where not N mod c
3
q)\t:10 i:count c; while[N mod c i-:1;]; c i
489
```

## Edge case

What if `N` is prime? Then it must be the last factor tried:

```q
C:N,c
last {x where not N mod x} C          / vector
i:count C; while[N mod C i-:1;]; C i  / While loop
```
