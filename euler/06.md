# 6. Sum square difference

https://projecteuler.net/problem=6

> The sum of the squares of the first ten natural numbers is 385
> The square of the sum of the first ten natural numbers is 3025
> Hence the difference between the sum of the squares of the first ten natural numbers and the square of the sum is 
>	3025 − 385 = 2640
> Find the difference between the sum of the squares of the first one hundred natural numbers and the square of the sum.


## Solutions

This is close to trivial in q.

```q
nnt:1+til@  / natural numbers to x
sqr:{x*x}
{(sqr sum x)-sum sqr x} nnt 100
```

## Alternatives

We can explore some alternatives. 

The square of the sums, the sum of the squares – two compositions?

```q
.[-](sqr sum@;sum sqr@)@\:nnt 100
```

We can factor out the repetitions…

```q
.[-](('[;]over) each 1 reverse\({x*x};sum) )@\: 1+til 100
```

… but it does nothing to improve readability.

---

Contributors

* Stephen Taylor